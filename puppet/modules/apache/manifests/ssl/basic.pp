class apache::ssl::basic inherits apache::ssl::minimal {

  # Listen on $http_listen_address, or all interfaces if unspecified
  $real_http_listen_address = default_value($http_listen_address, '*')

  # Reinstate ssl fragment with our preferred basic port-443 configuration
  Fragment['ssl'] {
    ensure  => present,
    content => template('apache/ssl.conf.erb'),
  }

  # Our fragment above specifies an SSLCertificateChainFile which won't be
  # present if the host is running with a self-signed certificate (e.g. one
  # generated by mod_ssl's postinstall scriptlet). This hackish exec allows
  # httpd to startup in this case (even though it hopefully never happens in
  # production) by just linking the default chain file to the default cert
  # file.
  exec { 'apache-ssl-basic-link-dummy-cert-chain':
    user    => 'root',
    cwd     => '/etc/pki/tls/certs',
    command => 'ln -s localhost.crt server-chain.crt',
    creates => '/etc/pki/tls/certs/server-chain.crt',
    onlyif  => 'test -f /etc/pki/tls/certs/localhost.crt',
    require => Package['mod_ssl'],
  }

}
